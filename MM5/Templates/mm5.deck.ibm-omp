#!/bin/csh
## for serial / OMP job
# @ environment = COPY_ALL;MP_EUILIB=us
# @ job_name   = serialjob
# @ output     = mm5.out
# @ error      = mm5.err
# @ node_usage = not_shared
# @ checkpoint = no
# @ wall_clock_limit = 5000
# @ class      = com_reg
# @ queue

### Uncomment this if you are using OMP
###set OMP_NUM_THREADS = 4


set TMPDIR = /ptmp/mm5$$
mkdir $TMPDIR
cd $TMPDIR


############################################################################
#####         IBM batch job for MM5 serial / OMP model runs ONLY       #####
#####                                                                  #####
#####  This deck has been set up to run on NCAR's IBM machines.        #####
#####  If you are not working with NCAR's IBM machines, this deck will #####
#####  not be much use to you.                                         #####
#####                                                                  #####
#####  This code expects to find the input files on mass storage.      #####
#####                                                                  #####
#####  If you are using OMP, uncomment 'set OMP_NUM_THREADS = 4'       #####
#####  above, and change the compilation options in section 3 of       #####
#####  configure.user file below, accordingly.                         #####
#####                                                                  #####
#####   This script is still in the experimental phase, and cover      #####
#####   only the basic features of the MM5 model.                      #####
#####   Capabilities and options are likely to change.                 #####
#####               Please be patient.                                 #####
#####                                                                  #####
#####   Questions and comments should be directed to:                  #####
#####                        mesouser@ucar.edu                         #####
#####                                                                  #####
############################################################################

#
#       this should be the user's case or experiment (used in MS name)
#
  set ExpName = MM5V3/TEST            # MSS path name for output
  set InName  = MM5V3/TEST/DATA       # MSS path name for input
  set RetPd   = 365                   # MSS retention period in days

#
#       type of mm5 job
#
  set compile   = yes         # if yes, compile the mm5 code
# set compile   = no          # if no, the deck expects to read mm5.exe

  set execute   = yes         # if yes, execute model run
# set execute   = no          # if no, complie only
#
# set STARTsw   = NoReStart   # start model run at hour 0
  set STARTsw   = ReStart     # restart model run
#
  if ( $STARTsw == ReStart ) then
     set compile = no
  endif

# set UseMySource = no        # Use "standard" version of MM5
  set UseMySource = yes       # Use my version of MM5
#
  set SourceCode = /home/username
                              # Path to my mm5.tar and mpp.tar files
#
  set FDDAsw    = NoFDDA      # no FDDA input files
# set FDDAsw    = Anly        # gridded FDDA input files
# set FDDAsw    = Obs         # obs FDDA input files
# set FDDAsw    = Both        # gridded and obs FDDA input files
#
#------------------------------------------------------------------------------
#
#    Input files:
#        1. lateral and lower boundary files
#
  set InBdy  =  ( BDYOUT_DOMAIN1 )
  set InLow  =  ( LOWBDY_DOMAIN1 LOWBDY_DOMAIN2 )
#
#        2. initial conditions and/or nest terrain file
#
# set InMM    = ( MMINPUT_DOMAIN1 )
  set InMM    = ( MMINPUT_DOMAIN1 \
                  TERRAIN_DOMAIN2 \
                  MMINPUT_DOMAIN2 )
#
#        3. input restart file - if this is a restart run
#
  if ( $STARTsw == ReStart ) then
     set InRst    = ( SAVE_DOMAIN1 \
                      SAVE_DOMAIN2 )
  endif
#
#        4. 4dda surface analyses - if this is a 4DDA run
#
if (( $FDDAsw == Anly ) || ( $FDDAsw == Both )) then
  set In4DSfc  = ( SFCFDDA_DOMAIN1 \
                   SFCFDDA_DOMAIN2  )
endif
#
#        5. 4dda observations - if this is a 4DDA run
#
if (( $FDDAsw == Obs  ) || ( $FDDAsw == Both )) then
  set In4DObs  = ( MM5OBS_DOMAIN1 \
                   MM5OBS_DOMAIN2 )
endif
#
#-----------------------------------------------------------------------------
#                 Get source code
#
if ( $compile == yes ) then
  if ( $UseMySource == yes ) then
    echo "Making use of own source code"
    cp $SourceCode/mm5.tar .
    tar -xvf mm5.tar
    rm mm5.tar
    cd MM5
  else
    echo "Getting source code from mass storage"
    msread mm5v3.tar.gz /MESOUSER/MM5V3/MM5.TAR.gz
    gunzip mm5v3.tar
    tar -xvf mm5v3.tar
    rm mm5v3.tar
    cd MM5
  endif
else
  echo "Compile is no, so get compiled code from mass storage"
  mkdir MM5
  cd MM5
  msread mm5exe.tar $ExpName/mm5exe.tar
  tar -xvf mm5exe.tar 
  rm mm5exe.tar
endif
  
#
#========================================================================
# configure.user file - User editting required for sections 5 and 6
#  **Please read Release-note for how to make changes in configure.user
#  *** Edit section 3 if you are using OMP
#------------------------------------------------------------------------
#
cat >! ./configure.user << "EOF"
# Sections
# 1. System Variables
# 3. Fortran Options
# 4. General commands
# 5. Options for making "./include/parame.incl"
# 6. Physics Options (memory related)
#
#-----------------------------------------------------------------------------
# 1. System Variables 
#-----------------------------------------------------------------------------
SHELL = 	/bin/sh
.SUFFIXES: .F .i .o .f .c
#-----------------------------------------------------------------------------
# 3. Fortran options
#    Uncomment the ones you need, including RUNTIME_SYSTEM
#-----------------------------------------------------------------------------
LIBINCLUDE = $(DEVTOP)/include
#-----------------------------------------------------------------------------
#    3f. IBM (AIX)
#-----------------------------------------------------------------------------
RUNTIME_SYSTEM = "IBM"
FC = xlf
FCFLAGS = -I$(LIBINCLUDE) -O3 -qarch=auto -qmaxmem=-1
CPP = /usr/lib/cpp
CFLAGS =
CPPFLAGS = -I$(LIBINCLUDE) -C -P -Drs6000
LDOPTIONS = -qmaxmem=-1 -O3 -qarch=auto
#LOCAL_LIBRARIES = -lmass
LOCAL_LIBRARIES =
MAKE = make -i
#-----------------------------------------------------------------------------
#    3f2. IBM (AIX)
#       - Depending on problem size and machine memory size, the settings
#         of maxstack and maxdata may need to be modified.
#       - If the newer thread-safe mass library is available, add
#         the -lmass_r option to LOCAL_LIBRARIES.
#-----------------------------------------------------------------------------
#RUNTIME_SYSTEM = "IBM"
#FC = xlf_r
#FCFLAGS = -I$(LIBINCLUDE) -O2 -qarch=auto -qmaxmem=-1 -qsmp=noauto -qnosave -qstrict
#CPP = /usr/lib/cpp
#CFLAGS =
#CPPFLAGS = -I$(LIBINCLUDE) -C -P -Drs6000
#LDOPTIONS = -qmaxmem=-1 -O2 -qarch=auto -bmaxstack:512000000 -bmaxdata:2000000000
#LOCAL_LIBRARIES = -lxlsmp -lmass_r
#LOCAL_LIBRARIES = -lxlsmp
#MAKE = make -i
#-----------------------------------------------------------------------------
# 4. General commands
#-----------------------------------------------------------------------------
AR = ar ru
RM = rm -f
RM_CMD = $(RM) *.CKP *.ln *.BAK *.bak *.o *.i core errs ,* *~ *.a \
.emacs_* tags TAGS make.log MakeOut *.f !
GREP = grep -s
CC = cc
#-----------------------------------------------------------------------------
# 5. Options for making ./include/parame.incl
#-----------------------------------------------------------------------------
#
# FDDAGD (integer)                  - "1" -> FDDA gridded run
FDDAGD = 0
#
# FDDAOBS (integer)                 - "1" -> FDDA obs run
FDDAOBS = 0
#
# MAXNES (integer)                  - Max Number of Domains in simulation
MAXNES = 2
#
# MIX,MJX (integer)                 - Maximum Dimensions of any Domain
MIX = 49
MJX = 52
# MKX (integer)                     - Number of half sigma levels in model
MKX = 23
#-----------------------------------------------------------------------------
# 6. Physics Options
#    The first MAXNES values in the list will be used for the corresponding
#        model nests; the rest in the list can be used to compile other options.
#        The exception is FRAD, of which only the first value is used in the model,
#        (i.e., only one radiation option is used for all nests). The rest allow 
#        other options to be compiled.
#    Compilation of Arakawa-Schubert cumulus scheme requires imsl.
#-----------------------------------------------------------------------------
# IMPHYS - for explicit moisture schemes (array,integer)
IMPHYS = "4,4,1,1,1,1,1,1,1,1"      
#                                   - Dry,stable,warm rain,simple ice,mix phase,
#                                   - 1  ,2     ,3        ,4         ,5
#                                   - graupel(gsfc),graupel(reisner2),schultz
#                                   -,6            ,7                ,8
MPHYSTBL = 0
#                                   - 0=do not use look-up tables for moist 
#                                     physics
#                                   - 1=use look-up tables for moist physics 
#                                     (currently only simple ice and mix phase 
#                                      are available)
#
# ICUPA - for cumulus schemes (array,integer)
#                                   - None,Kuo,Grell,AS,FC,KF,BM,KF2 - 1,2,3,4,5,6,7,8
ICUPA  = "3,3,1,1,1,1,1,1,1,1"
#
# IBLTYP - for planetary boundary layer (array,integer)
#                                   - 0=no PBL fluxes,1=bulk,2=Blackadar,
#                                     3=Burk-Thompson,4=Eta M-Y,5=MRF,
#                                     6=Gayno-Seaman,7=Pleim-Xiu
IBLTYP  = "5,5,0,0,0,0,0,0,0,0"
#
# FRAD - for atmospheric radiation (integer)
#                                   - Radiation cooling of atmosphere 
#                                     0=none,1=simple,2=cloud,3=ccm2,4=rrtm
FRAD = "2,0,0,0,0"
#
# ISOIL - for multi-layer soil temperature model (integer)
#                                   - 0=no,1=yes (only works with IBLTYP=2,4,5,6)
#                                     2=OSU land-surface scheme (IBLTYP=5 only)
#                                     3=Pleim-Xiu LSM (IBLTYP=7 only)
ISOIL = 1
#
# ISHALLO (array,integer)           - Shallow Convection Option
#                                     1=shallow convection,0=No shallow convection
ISHALLO  = "0,0,0,0,0,0,0,0,0,0"
#-----------------------------------------------------------------------------
# Don't touch anything below this line
#-----------------------------------------------------------------------------
.F.i:
	$(RM) $@
	$(CPP) $(CPPFLAGS) $*.F > $@
	mv $*.i $(DEVTOP)/pick/$*.f
	cp $*.F $(DEVTOP)/pick
.c.o:
	$(RM) $@ && \
	$(CC) -c $(CFLAGS) $*.c

.F.o:
	$(RM) $@
	$(FC) -c $(FCFLAGS) $*.F

.F.f:
	$(RM) $@
	$(CPP) $(CPPFLAGS) $*.F > $@

.f.o:
	$(RM) $@
	$(FC) -c $(FCFLAGS) $*.f
#-----------------------------------------------------------------------
"EOF"
#
#=======================================================================
# Options for namelist ("mmlif") - User editting required
#   (Comments in the namelist are removed during execution)
#-----------------------------------------------------------------------
#
#	The first dimension (column) of the arrays denotes the domain
#	identifier.
#		Col 1 = Domain #1, Col 2 = Dom #2, etc.
#
cat > ./Run/oparam << EOF
 &OPARAM
#INCLUDE oparam
 /
EOF
cat > ./Run/lparam << EOF
 &LPARAM
#INCLUDE lparam
EOF
cat > ./Run/nparam << EOF
 &NPARAM
#INCLUDE nparam
 /
EOF
cat > ./Run/pparam << EOF
 &PPARAM
#INCLUDE pparam
 /
EOF
cat > ./Run/fparam << EOF
 &FPARAM
#INCLUDE fparam
 /
EOF
#
#----------------------------------------------------------------------------
#                    Make code
if ( $compile == yes ) then
  echo "CODE are now being compiled"
  make 
  tar -cvf mm5exe.tar * 
  echo "mswrite executable files"
  mswrite -t $RetPd mm5exe.tar $ExpName/mm5exe.tar
else
  echo "Code will not be COMPILED"
endif
#----------------------------------------------------------------------------
#
#      create namelist: mmlif, and remove comments from namelist:
#
if ( $execute == no ) then
  echo "NO execution"
  exit
endif

echo "RUN model"

make mmlif
cd ./Run
sed -f ../Util/no_comment.sed mmlif | grep "[A-Z,a-z,/]" > mmlif.tmp
mv mmlif.tmp mmlif
rm fparam lparam nparam oparam pparam
#----------------------------------------------------------------------------
#
#       get boundary conditions from MS
#
   foreach fil ( $InBdy )
       echo "msread " $fil
       msread $fil $InName/$fil
   end
   foreach fil ( $InLow )
       echo "msread " $fil
       msread $fil $InName/$fil
   end
#
#       1. initial conditions
#
   foreach fil ( $InMM )
       echo "msread " $fil
       msread $fil $InName/$fil
   end

set NUMFIL = 1
while ( $NUMFIL <= ${#InMM} )
   if (( $FDDAsw == Anly ) || ( $FDDAsw == Both ) && ( -e MMINPUT_DOMAIN$NUMFIL)) then
      cp MMINPUT_DOMAIN$NUMFIL MMINPUT2_DOMAIN$NUMFIL
   endif
   @ NUMFIL ++
end
#
#       2. input restart conditions
#
if ( $STARTsw == ReStart ) then
   set NUMFIL = 1
   while ( $NUMFIL <= ${#InRst} )
      msread RESTART_DOMAIN$NUMFIL $ExpName/$InRst[${NUMFIL}]
      @ NUMFIL ++
   end
endif

#
#       3. get surface analyses files for nudging
#
if (( $FDDAsw == Anly ) || ( $FDDAsw == Both )) then
   foreach fil ( $In4DSfc )
       echo "msread " $fil
       msread $fil $InName/$fil
   end
endif
#
#       4. observations if OBS nudging
#
if (( $FDDAsw == Obs ) || ( $FDDAsw == Both )) then
   foreach fil ( $In4DObs )
       echo "msread " $fil
       msread $fil $InName/$fil
   end
endif

#----------------------------------------------------------------------------
#
#       run MM5
#

timex ./mm5.exe > mm5.print.out

#
#       Write to MSS all output files
#
ls -ls
foreach file ( MMOUT_DOMAIN* SAVE_DOMAIN* )
   echo "mswrite" $file
   mswrite -t $RetPd $file $ExpName/$file
end
#
if ( -e fort.26 ) then
   set NUM = 1
   foreach file ( fort.2? )
      echo "mswrite time series data"
      mswrite -t $RetPd $file $ExpName/TIMESERIES_DOMAIN$NUM
      @ NUM++
   end
endif
#
   tar -cvf mm5out.tar mm5.print.out mmlif
   echo " mswrite -t $RetPd mm5out.tar $ExpName/mm5out.tar "
   mswrite -t $RetPd mm5out.tar $ExpName/mm5out.tar


###Clean up
##cd $TMPDIR
##cd ..
##rm -r $TMPDIR
##cd

echo "DONE"

exit
#ENDTEMPLATE
